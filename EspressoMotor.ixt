'[VARIABLES]
'none

'[PROGRAMS]
'Enter program mode at address 1
PG 1
 '*********MAIN PROGRAM*********
  'label re-entry point
  LB A1
  'READ/WRITE VARS
  'set slew velocity to 0 just in case
  SL=0
  'Define global stability var. Init to 10
  F5=10
  'Define global temperature
  F3=0
  'Define global temporary temperature used for stability calc
  F4=0
  'initialize button held time accumulator to 0
  BH=0
  'initialize backflush flag to 0
  BF=0
  'initialize backflush flip flop time accumulator to 0
  FF=0 
   
  'Call subroutine B0 to recirc and heat if button pressed
  CL B0, I2=1
  'Re-enter at A1(i.e. loop)
  BR A1

'*********SUBROUTINES**********
LB B0  
  'set liquid sensor input as soft stop
  IS=3,5,1
  'call B1 to check if backflush routine
  CL B1
  'call standard extraction routine
  CL B6
  'set liquid sensor input back to GPIO
  IS=3,0,1
RT

'subroutine loop to check for backflush routine
LB B1
  'pause for 50 ms
  H 50
  'increment button held accumulator 
  BH = BH+50
  'Call B2 if held for more than 2 seconds
  CL B2, BH > 2000
  'Loop if button is still pressed
  BR B1, I2=1
RT

'subroutine to set backflush routine flag and run backflush
LB B2
  'set backflush flag high
  BF=1
  'close recirc valve
  O3=0
  'open group head valve, i.e. backflush vent closed
  O2=1
  'start running pump at backflush speed
  SL=S7
  'Increment accumulator variable 50 ms which determines how long to build up pressure
  FF=FF+50
  'Pause for 50ms
  H 50
  'Call B3 if pressurized, i.e. pressurized for variable seconds
  CL B3, FF > L8
  'loop if still pressing button
  BR B2, I2=1
  'call B4 once reached this line
  CL B3
RT

'subroutine to vent group solenoid
LB B3
  'stop motor
  SL=0
  'wait some for it to stop
  H 750
  'close group solenoid, i.e. open grouphead backflush vent 
  O2=0
  'hold for total pressure venting
  H 1000
  'reset flip flop accumulator
  FF=0
RT

LB B6
  'Call heating loop if not backflush
  CL B7, BF=0
RT

'subroutine that starts heating loop
LB B7 
  'open recirc solenoid
  O3=1
  'start pumping for recirculation
  SL=S8
  'hold to allow for total priming
  H 15000
  'turn heater on
  O1=1
  'Re-entry for checking temp and stability
  LB B8   
    'delay for 5 seconds
    H 2500
    'branch/loop back to B8 if temp switch is not active
    BR B8, I1=0

   
    'EXTRACTION ROUTINE
    'delay to allow group head to get up to temp
    H T9
    'turn group head solenoid on
    O2=1
    'pause for pre-infusion
    H T7
    'turn group head solenoid off to allow soak
    O2=0
    'Hold for soak 
    H T6
    'open group again
    O2=1
    'now ready to extract
    'turn heater off
    O1=0
    'turn recirc solenoid off
    O3=0
    'speed up pump
    SL=S9
    'hold for x seconds for extraction
    H T8
    'stop pumping
    SL=0
    'hold for 10 seconds to allow pressure to drop
    H 10000
    'vent to reservoir
    O3=1
    'Pause to wait for drop to 0 psi
    H 3000
    'turn group head solenoid off
    O2=0
RT


'end and exit program 
E

'*********START UP**************
'Start up routine
LB SU
  'Set outputs LOW 
	ER=0
    O1=0
    O2=0
    O3=0
    EX A1
E
PG



