'[VARIABLES]
'none

'[PROGRAMS]
'Enter program mode at address 1
PG 1

'*********MAIN PROGRAM*********
'label re-entry point
LB A1
	'READ/WRITE VARS
	'set slew velocity to 0 just in case
	SL=0
	'Define global stability var. Init to 10
	F5=10
	'Define global temperature
	F3=0
	'Define global temporary temperature used for stability calc
	F4=0
	'global flag that indicates if heating loop was performed successfully
	L8=0
	'initialize button held duration to 0
	Bh=0
	'initialize backflush flag to 0
	Bf=0
  
	'Call subroutine B0 to recirc and heat if liquid sensed and button pressed
	CL B0, I2=1
	'call C1 to run extraction routine if heating loop flag set to 1 
	CL C8, L8=1
	'Re-enter at A1(i.e. loop)
	BR A1

'*********SUBROUTINES**********
LB B0
	'call B1 to check if backflush routine
	CL B1
	'call B3 if liquid present(not implemented)
    CL B4, I3=1
    RT
	
'subroutine loop to check for backflush routine
LB B1
	'pause for 50 ms
	H 50
	'increment button held accumulator 
	Bh = Bh+50
	'Call B2 if held for more than 2 seconds
	CL B2, Bh > 2000
	'Loop if button is still pressed
	BR B1, I2=1
	RT

'subroutine to set backflush routine flag and run backflush
LB B2
	'set backflush flag high
	Bf=1
	'close recirc valve
	O3=0
	'ensure group head valve closed, i.e. backflush vent open
	O2=0
	'start running pump at recirc speed
	SL=S8
	'loop if still pressing button
	BR B2, I2=1
	'call B3 if not pressing button
	CL B3, I2=0
	RT

'subroutine to stop motor
LB B3
	'Set slew velocity to 0
	SL=0
	RT

LB B4
	'Call heating loop if not backflush
	CL B5, Bf=0
	RT
	
'subroutine that starts heating loop
LB B5
  'open recirc solenoid
  O3=1
  'start pumping for recirculation
  SL=S8
  'hold to allow for total priming
  H 15000
  'turn heater on
  O1=1
  'Re-entry for checking temp and stability
  LB B2
    'Read analog IN multiply slope add y-intercept to get deg F
    F3=F1*I5+F2
    'Calculate by absolute diff of last val and current
    F5=F3-F4
    F5=Ab F5
    'Store temperature in temporary var
    F4=F3
    'Calculate absolute diff of current temp and set point temp
    F6=T1-F3
    F6=Ab F6    
    'delay for 5 seconds
    H 5000
    'branch/loop back to B2 if temp diff is greater than set point tolerance 
    'OR if temp diff is larger than acceptable
    BR B2, F6 > R9
    BR B2, F5 > R0
    'set heating loop flag if made it to this line
    L8=1
    'return when conditions met
    RT


'subroutine that starts pumping to group head
LB C8
  'delay to allow group head to get up to temp
  H T9
  'turn heater off
  O1=0
  'turn group head solenoid on
  O2=1
  'turn recirc solenoid off
  O3=0
  'speed up pump
  SL=S9
  'hold for 6 seconds
  H T8
  'stop pumping
  SL=0
  'hold for 10 seconds to allow pressure to drop
  H 10000
  'vent to reservoir
  O3=1
  'Pause to wait for drop to 0 psi
  H 3000
  'turn group head solenoid off
  O2=0
  'return to main program
  RT


'end and exit program 
E

'*********START UP**************
'Start up routine
LB SU
  'Set outputs LOW 
    O1=0
    O2=0
    O3=0
    EX A1
E
PG



