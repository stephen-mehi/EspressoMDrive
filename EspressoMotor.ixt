'Enter program mode at address 1
PG 1
'*********MAIN PROGRAM*********
'label re-entry point
LB G1
  'READ/WRITE VARS
  'set slew velocity to 0 just in case
  SL=0
  'initialize button held time accumulator to 0
  Q4=0
  'initialize backflush flag to 0
  Q3=0
  'initialize backflush flip flop time accumulator to 0
  Q2=0 
  'Initialize delay loop counter to 0
  Q8=0
  'Call subroutine K0 to recirc and heat if button pressed
  CL K0, I2=1
  'Re-enter at G1(i.e. loop)
  BR G1

'*********SUBROUTINES**********
LB K0
  'call K1 to check if backflush routine
  CL K1
  'call standard extraction routine
  CL K4
RT

'subroutine loop to check for backflush routine
LB K1
  'pause for 50 ms
  H 50
  'increment button held accumulator 
  Q4 = Q4+50
  'Call K2 if held for more than 2 seconds
  CL K2, Q4 > 2000
  'Loop if button is still pressed
  BR K1, I2=1
RT

'subroutine to set backflush routine flag and run backflush
LB K2
  'set backflush flag high
  Q3=1
  'close recirc valve
  O3=0
  'open group head valve, i.e. backflush vent closed
  O2=1
  'start running pump at backflush speed
  SL=Q6
  'Increment accumulator variable 50 ms which determines how long to build up pressure
  Q2=Q2+50
  'Pause for 50ms
  H 50
  'Call K3 if pressurized, i.e. pressurized for variable seconds
  CL K3, Q2 > Q1
  'loop if still pressing button
  BR K2, I2=1
  'call B4 once reached this line
  CL K3
RT

'subroutine to vent group solenoid
LB K3
  'stop motor
  SL=0
  'wait some for it to stop
  H 750
  'close group solenoid, i.e. open grouphead backflush vent 
  O2=0
  'hold for total pressure venting
  H 1000
  'reset flip flop accumulator
  Q2=0
RT

LB K4
  'Call heating loop if not backflush
  CL K5, Q3=0
RT

'subroutine that starts heating loop
LB K5
  'open recirc solenoid
  O3=1
  'start pumping for recirculation
  SL=Q5
  'hold to allow for total priming
  H 15000
  'turn heater on
  O1=1
  'Re-entry for checking temp and stability
  LB K6
    'delay
    H 250
    'branch/loop back to K6 if temp switch is not active
    BR K6, I1=0

	'reentry point for delaying
    LB K7
	  H 1000
	  'increment loop count
	  Q8 = Q8 + 1000
	  'branch back to K7 if need to keep delaying
	  BR K7, Q7 > Q8
	
    'EXTRACTION ROUTINE

    'turn group head solenoid on
    O2=1
    'pause for pre-infusion
    H Q10
    'turn group head solenoid off to allow soak
    O2=0
    'Hold for soak 
    H Q11
    'open group again
    O2=1
    'now ready to extract
    'turn heater off
    O1=0
    'turn recirc solenoid off
    O3=0
    'speed up pump
    SL=Q12
    'hold for x seconds for extraction
    H Q9
    'stop pumping
    SL=0
    'hold for 10 seconds to allow pressure to drop
    H 10000
    'vent to reservoir
    O3=1
    'Pause to wait for drop to 0 psi
    H 3000
    'turn group head solenoid off
    O2=0
RT

'*********START UP**************
'Start up routine
LB SU
  H 500
  'Set outputs LOW 
  O1=0
  O2=0
  O3=0
  IS=3,5,1
  EX G1
PG
